//tag::collectIncludes[]
import static groovy.io.FileType.*
import java.security.MessageDigest

task collectIncludes(
        description: 'collect all ADRs as includes in one file',
        group: 'docToolchain'
) {
    doFirst {
        new File(targetDir, '_includes').mkdirs()
    }
    doLast {
        //let's search the whole project for files, not only the docs folder
        //could be a problem with node projects :-)
        File sourceFolder = project(':').projectDir
        println "sourceFolder: " + sourceFolder.canonicalPath
        def collections = [:]
        sourceFolder.traverse(type: FILES) { file ->
            if (file.name ==~ '^[A-Z]{3,}-.*[.](ad|adoc|asciidoc)$') {
                def type = file.name.replaceAll('^([A-Z]{3,})-.*$','\$1')
                if (!collections[type]) {
                    collections[type] = []
                }
                def fileName = ((file.canonicalPath - sourceFolder.canonicalPath)[1..-1])
                if (fileName.startsWith('docToolchain')) {
                    //ignore docToolchain as submodule
                } else {
                    collections[type] << fileName
                }
            }
        }
        def pathDiff = '../' * ((targetDir - sourceFolder.canonicalPath)
                                    .replaceAll('^/','')
                                    .replaceAll('/$','')
                                    .replaceAll("[^/]",'').size()+1)
        collections.each { type, fileNames ->
            if (fileNames) {
                def outFile = new File(targetDir+'/_includes', type+'_includes.adoc')
                println outFile.canonicalPath-sourceFolder.canonicalPath
                outFile.write("// this is autogenerated\n")
                fileNames.each { fileName ->
                    outFile.append ("include::../"+pathDiff+fileName.replace("\\", "/")+"[]\n\n")
                }
            }
        }
    }
}


//end::collectIncludes[]

